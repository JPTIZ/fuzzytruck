function_block time

var_input
    temperature: real;
    cloud_density: real;
end_var

var_output
    will_rain: real;
end_var

fuzzify temperature
    term cold := ( 0, 1) (15, 0);
    term warm := (10, 0) (20, 1) (30, 0);
    term hot  :=         (20, 0) (30, 1);
end_fuzzify

fuzzify cloud_density
    term no_clouds := (0.0 , 1) (0.25, 0);
    term sparse    := (0.15, 0) (0.30, 1) (0.60, 0);
    term cloudy    :=           (0.55, 0) (1.00, 1);
end_fuzzify

defuzzify will_rain
    term no       := (0.0 , 1.0) (0.55, 0.0);
    term a_little := (0.45, 0.0) (0.60, 1.0) (0.75, 0.0);
    term a_lot    := (0.65, 0.0) (0.70, 0.9) (1.00, 1.0);

    method: cog;
    default := 0;
end_defuzzify

ruleblock time_rules
    and: min;
    act: min;
    accu: max;

    rule 1: if temperature is warm and cloud_density is cloudy then will_rain is a_little;
    rule 2: if temperature is hot  and cloud_density is cloudy then will_rain is a_lot   ;
    rule 3: if temperature is hot  and cloud_density is sparse then will_rain is a_little;
end_ruleblock

end_function_block
